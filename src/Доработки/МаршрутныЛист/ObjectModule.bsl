Перем КонтекстЯдра;
Перем Ожидаем;
Перем Утверждения;
Перем ГенераторТестовыхДанных;
Перем ЗапросыИзБД;
Перем УтвержденияПроверкаТаблиц;
Перем РаботаСДокументами;

#Область ЮнитТестирование

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	ГенераторТестовыхДанных = КонтекстЯдра.Плагин("СериализаторMXL");
	ЗапросыИзБД = КонтекстЯдра.Плагин("ЗапросыИзБД");
	УтвержденияПроверкаТаблиц = КонтекстЯдра.Плагин("УтвержденияПроверкаТаблиц");
	
	РаботаСДокументами = КонтекстЯдра.Плагин("Plugin_РаботаСДокументами");
	
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	
	НаборТестов.Добавить("ЗаполнениеНормативовПриСменеХарактеристики");
	
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнениеНормативовПриСменеХарактеристики() Экспорт
	
	Структура = ГенераторТестовыхДанных.СоздатьДанныеПоМакетам(ЭтотОбъект, "Ссылки");
	РаботаСДокументами.УдалитьДокументыПоОрганизации(Структура.Организация);
	
	СтруктураДанных = ГенераторТестовыхДанных.СоздатьДанныеПоМакетам(ЭтотОбъект, "ЗаполнениеНормативовПриСменеХарактеристики");
	
	ДокументОбъект = СтруктураДанных.ЗаказНаПроизводство.ПолучитьОбъект();
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение); //ЗаказНаПроизводство
	
	ДокументОбъект = СтруктураДанных.МаршрутныйЛист.ПолучитьОбъект();
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение); //МаршрутныйЛист
	
	// Создание 2 МЛ
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Распоряжение",  ДокументОбъект.Распоряжение);
	ДанныеЗаполнения.Вставить("Подразделение", ДокументОбъект.Подразделение);
	ДанныеЗаполнения.Вставить("УправлениеМаршрутнымиЛистами", ДокументОбъект.УправлениеМаршрутнымиЛистами);
	ДанныеЗаполнения.Вставить("КодСтроки", ДокументОбъект.КодСтроки);
	ДанныеЗаполнения.Вставить("КодСтрокиЭтапыГрафик", ДокументОбъект.КодСтрокиЭтапыГрафик);
	ДанныеЗаполнения.Вставить("Этап",      ДокументОбъект.Этап);
	ДанныеЗаполнения.Вставить("Начало",    ДокументОбъект.Начало);
	ДанныеЗаполнения.Вставить("Окончание", ДокументОбъект.Окончание);
	ДанныеЗаполнения.Вставить("НачалоРаботыКлючевогоРабочегоЦентра",    ДокументОбъект.НачалоРаботыКлючевогоРабочегоЦентраПоГрафику);
	ДанныеЗаполнения.Вставить("ОкончаниеРаботыКлючевогоРабочегоЦентра", ДокументОбъект.ОкончаниеРаботыКлючевогоРабочегоЦентраПоГрафику);
	ДанныеЗаполнения.Вставить("Запланировано", 1);
	ДанныеЗаполнения.Вставить("ДопроизводствоМаршрутногоЛиста", ДокументОбъект.Ссылка);
	
	ДокументОбъектНовый = Документы.МаршрутныйЛистПроизводства.СоздатьДокумент();
	ДокументОбъектНовый.Дата = '2017-10-01';
	ДокументОбъектНовый.Заполнить(ДанныеЗаполнения);
	
	СтрокаТЧ = ДокументОбъектНовый.МатериалыИУслуги[0];
	
	Утверждения.ПроверитьРавенство(СтрокаТЧ.Количество, 1.1, "Не верно рассчитался норматив");
	
КонецПроцедуры	
