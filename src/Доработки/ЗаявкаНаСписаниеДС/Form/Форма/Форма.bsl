&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Ожидаем;
&НаКлиенте
Перем Утверждения;
&НаКлиенте
Перем ГенераторТестовыхДанных;
&НаКлиенте
Перем ЗапросыИзБД;
&НаКлиенте
Перем УтвержденияПроверкаТаблиц;

#Область ЮнитТестирование

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	ГенераторТестовыхДанных = КонтекстЯдра.Плагин("СериализаторMXL");
	ЗапросыИзБД = КонтекстЯдра.Плагин("ЗапросыИзБД");
	УтвержденияПроверкаТаблиц = КонтекстЯдра.Плагин("УтвержденияПроверкаТаблиц");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ПоСчетам");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоСчетам() Экспорт
	
	Макет = ПолучитьМакет();
	СтруктураДанных = ГенераторТестовыхДанных.СоздатьДанныеПоТабличномуДокументу(Макет,,, Истина);
	ПровестиДокумент(СтруктураДанных.ЗаказПоставщику1);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", СтруктураДанных.ЗаказПоставщику1);
	
	Форма = ПолучитьФорму("Документ.ЗаявкаНаРасходованиеДенежныхСредств.ФормаОбъекта", ПараметрыФормы);
	Форма.Открыть();
	
	Утверждения.ПроверитьРавенство(Форма.Элементы._ЗаполнитьНазначениеПоСчетам.Видимость, Истина, "Нет кнопки По счетам");
	
	Форма.Объект.Комментарий = "за товары";
	Форма._ЗаполнитьНазначениеПоСчетам(Неопределено);
	
	НазначениеПлатежа = Форма.Объект.НазначениеПлатежа;
	НазначениеПлатежаОригинал = 
	"Оплата по счету №123456 от 01.01.2016 за товары 1180-00 руб.
	|В т.ч. НДС (18%) 180-00 руб.";
	
	Форма.Закрыть();
	
	Утверждения.ПроверитьРавенство(НазначениеПлатежа, НазначениеПлатежаОригинал, "Не установилось назначение платежа");
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ПровестиДокумент(Ссылка)
	
	ДокументОбъект = Ссылка.ПолучитьОбъект();
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры	

&НаСервере
Функция ПолучитьМакет()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОбъект.ПолучитьМакет("Данные");
	
КонецФункции

