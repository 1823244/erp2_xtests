Перем КонтекстЯдра;
Перем Ожидаем;
Перем Утверждения;
Перем ГенераторТестовыхДанных;
Перем ЗапросыИзБД;
Перем УтвержденияПроверкаТаблиц;

Перем РаботаСДокументами;

#Область ЮнитТестирование

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	ГенераторТестовыхДанных = КонтекстЯдра.Плагин("СериализаторMXL");
	ЗапросыИзБД = КонтекстЯдра.Плагин("ЗапросыИзБД");
	УтвержденияПроверкаТаблиц = КонтекстЯдра.Плагин("УтвержденияПроверкаТаблиц");
	
	РаботаСДокументами = КонтекстЯдра.Плагин("Plugin_РаботаСДокументами");
	
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	
	НаборТестов.Добавить("ПечатнаяФормаСчетФактура");
	
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ПечатнаяФормаСчетФактура() Экспорт
	
	Структура = ГенераторТестовыхДанных.СоздатьДанныеПоМакетам(ЭтотОбъект, "Организация");
	РаботаСДокументами.УдалитьДокументыПоОрганизации(Структура.Организация);
	
	СтруктураДанных = ГенераторТестовыхДанных.СоздатьДанныеПоМакетам(ЭтотОбъект, "Данные");
	
	РаботаСДокументами.ПровестиДокумент(СтруктураДанных.ПоступлениеТоваровУслуг1);
	РаботаСДокументами.ПровестиДокумент(СтруктураДанных.РеализацияТоваровУслуг1);
	
	СформироватьСчетФактуруПоРеализации(Структура.Организация, СтруктураДанных.РеализацияТоваровУслуг1);
	
	Оригинал = ПолучитьМакет("СчетФактура");
	ТабДок = СформироватьПечФормуСчетФактура(СтруктураДанных.РеализацияТоваровУслуг1);
	
	УтвержденияПроверкаТаблиц.ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(Оригинал, ТабДок,,, "Не верная печ. форма");
	
КонецПроцедуры

//СчетФактура
Функция СформироватьСчетФактуруПоРеализации(Организация, ДокументРеализации)
	
	РеквизитыСчетаФактуры = Неопределено;
	ПараметрыОтбора = Новый Структура("Организация", Организация);
	СчетаФактуры = Документы.СчетФактураВыданный.СчетаФактурыПоОснованию(ДокументРеализации, ПараметрыОтбора, РеквизитыСчетаФактуры);
	
	Если СчетаФактуры.Количество() > 0 Тогда
		ДокументОбъект = РеквизитыСчетаФактуры.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ДокументыОснования.Очистить();
	Иначе
		ДокументОбъект = Документы.СчетФактураВыданный.СоздатьДокумент();
		ДокументОбъект.Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРеализации, "Дата");
	КонецЕсли;	
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ДокументОснование", ДокументРеализации);
	ДанныеЗаполнения.Вставить("Организация",       Организация);
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат ДокументОбъект;
	
КонецФункции	

Функция СформироватьПечФормуСчетФактура(Ссылка)
	
	СтруктраТипов = Новый Соответствие;
	
	Массив = Новый Массив;
	Массив.Добавить(Ссылка);
	
	СтруктраТипов.Вставить("Документ.РеализацияТоваровУслуг", Массив);
	
	ОбъектыПечати = Новый СписокЗначений;
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ПечатьВВалюте", Ложь);
	
	Возврат Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктраТипов, ОбъектыПечати, ПараметрыПечати);
	
КонецФункции
